plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 32
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.xuexiang.templateproject"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    signingConfigs {
        release {
            if (isNeedPackage.toBoolean()) {
                storeFile file(app_release.storeFile)
                storePassword app_release.storePassword
                keyAlias app_release.keyAlias
                keyPassword app_release.keyPassword
            }
        }
        debug {
            storeFile file("./debug.jks")
            storePassword "123456"
            keyAlias "debug"
            keyPassword "123456"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "APP_ID_UMENG", getUmengAppId()
        }

        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
            buildConfigField "String", "APP_ID_UMENG", '""'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.preference:preference:1.2.0'
    implementation files('libs\\jtds-1.3.1.jar')
    testImplementation('junit:junit:4.13.2') {
        exclude group: 'org.hamcrest'
    }
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // 其他依赖项保持不变
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation deps.androidx.multidex
    implementation 'com.alibaba.android:vlayout:1.3.0'
    implementation 'io.github.scwang90:refresh-layout-kernel:2.0.6'
    implementation 'io.github.scwang90:refresh-header-material:2.0.6'
    implementation 'com.github.xuexiangjys.AgentWeb:agentweb-core:1.0.0'
    implementation 'com.github.xuexiangjys.AgentWeb:agentweb-download:1.0.0'
    implementation 'com.tencent:mmkv:1.2.10'
    implementation 'me.jessyan:autosize:1.2.1'
    implementation 'com.umeng.umsdk:common:9.3.8'
    implementation 'com.umeng.umsdk:asms:1.2.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'me.samlss:broccoli:1.0.0'
    implementation 'com.zzhoujay.richtext:richtext:3.0.8'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.1'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'
    implementation 'com.meituan.android.walle:library:1.1.6'

    // LeakCanary 依赖
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:2.8.1'
}

// 自动添加X-Library依赖
apply from: 'x-library.gradle'
// walle多渠道打包
apply from: 'multiple-channel.gradle'

def getUmengAppId() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("APP_ID_UMENG") ?: '""'
}
